---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "rafay_workload Resource - terraform-provider-rafay"
subcategory: ""
description: |-
  
---

# rafay_workload (Resource)

A workload is an application that manages a set of pods.

## Example Usage
Workload example of Helm package type by uploading files from local system 

```terraform
resource "rafay_workload" "tftestworkload1" {
  metadata {
    name    = "tftestworkload1"
    project = "terraform"
  }
  spec {
    namespace = "test-workload1"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    drift {
      action  = "Deny"
      enabled = true
    }
    version = "v0"
    artifact {
      type = "Helm"
      artifact {
        chart_path {
          name = "file://relative/path/to/some/chart.tgz"
        }
        values_paths {
          name = "file://relative/path/to/some/chart.yaml"
        }
      }
    }
  }
}
```

---

Create workload of Helm package type from Helm repo.

```terraform
resource "rafay_workload" "tftestworkload2" {
  metadata {
    name    = "tftestworkload2"
    project = "terraform"
  }
  spec {
    namespace = "test-workload2"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    version = "v0"
    artifact {
      type = "Helm"
      artifact{
        values_paths {
          name = "file://relative/path/to/some/chart/values.yaml"
        }
        repository = "helm-repo-name"
        chart_name = "chartname"
        chart_version = "versionID"
      }
    }
  }
}
```

---

Create workload of Helm package type from git repo.

```terraform
resource "rafay_workload" "tftestworkload3" {
  metadata {
    name    = "tftestworkload3"
    project = "terraform"
  }
  spec {
    namespace = "test-workload3"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    version = "v0"
    artifact {
      type = "Helm"
      artifact {
        chart_path {
          name = "relative/path/to/some/chart.tgz"
        }
        repository = "git-user-repo-name"
        revision   = "branchname"
      }
    }
  }
}
```

---

Create a workload of K8s type by uploading from local system.

```terraform
resource "rafay_workload" "tftestworkload4" {
  metadata {
    name    = "tftestworkload4"
    project = "terraform"
  }
  spec {
    namespace = "test-workload4"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    version = "v0"
    artifact {
      type = "Yaml"
      artifact {
        paths {
          name = "file://relative/path/to/some/chart.yaml"
        }
      }
    }
  }
}
```

---

Create workload of K8s Yaml type from git repo.

```terraform
resource "rafay_workload" "tftestworkload5" {
  metadata {
    name    = "tftestworkload5"
    project = "terraform"
  }
  spec {
    namespace = "test-workload5"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    drift {
      action  = "Deny"
      enabled = true
    }
    version = "v0"
    artifact {
      type = "Yaml"
      artifact {
        paths {
          name = "yaml/workload.yaml"
        }
        repository = "release-check-ssh"
        revision   = "main"
      }
    }
  }
}
```

---

Create Helm workload from Git repo.
Chart and default values from one repo, override values from another repo.

```terraform
resource "rafay_workload" "tftestworkload6" {
  metadata {
    name    = "tftestworkload6"
    project = "terraform"
  }
  spec {
    namespace = "test-workload6"
    placement {
      selector = "rafay.dev/clusterName=cluster-1"
    }
    version = "v0"
    artifact {
      type = "Helm"
      artifact {
        repository = "test-repo1"
        revision   = "main"
        chart_path {
          name = "chart/path/chart.tgz"
        }
        #default value from same repo as chart
        values_paths {
          name = "value/path/values.yaml"
        }
        #override value from another repo
        values_ref {
          repository = "test-repo2"
          revision   = "main"
          values_paths {
            name = "value/path/values.yaml"
          }
        }
      }
    }
  }
}

```

<!-- schema generated by tfplugindocs -->
## Argument Reference

***Required***

- `metadata` - (Block List, Max: 1) Contains data that helps uniquely identify the resource. (See [below for nested schema](#nestedblock--metadata))
- `spec` - (Block List, Max: 1) Defines the characteristics for the resource. This is the desired state for the resource. (See [below for nested schema](#nestedblock--spec))


***Optional***

- `impersonate` (String) impersonate user
- `timeouts` - (Block) Sets the duration of time the create, delete, and update functions are allowed to run. If the function takes longer than this, it is assumed the function has failed. The default is 10 minutes. (See [below for nested schema](#nestedblock--timeouts))


<a id="nestedblock--metadata"></a>
### Nested Schema for `metadata`

***Required***

- `name` - (String) The name of the resource. This must be unique in your organization. 
- `project` - (String) The name of the Rafay project the workload will be created in. 


<a id="nestedblock--spec"></a>
### Nested Schema for `spec`

***Required***

- `artifact` (Block List, Max: 1) The workload artifact specification. (See [below for nested schema](#nestedblock--spec--artifact))
- `namespace` - (String) The namespace of the workload resource. 
- `placement` - (Block List, Max: 1) Defines the cluster(s) where workload will be created. (See [below for nested schema](#nestedblock--spec--placement))
- `version` - (String) The version of the workload resource. 

***Optional***

- `drift` - (Block List, Max: 1) Prevents configuration drift. Drift is a change to your live cluster that is different from the source of truth. (See [below for nested schema](#nestedblock--spec--drift))


<a id="nestedblock--spec--artifact"></a>
### Nested Schema for `spec.artifact`

***Required***

- `artifact` - (Block List, Max: 1) Contains data about the artifact repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact))
- `type` - (String) The type of artifact. Supported values are: `Helm` and `Yaml`.

***Optional***

- `options` - (Block List, Max: 1) Helm options. (See [below for nested schema](#nestedblock--spec--artifact--options))
 


<a id="nestedblock--spec--artifact--artifact"></a>
### Nested Schema for `spec.artifact.artifact`

***Required***

- `chart_name` - (String) The name of the chart. 
- `chart_path` - (Block List, Max: 1) The relative path to the chart file in the Git repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `chart_version` - (String) The version of the chart. 
- `paths` - (Block List) The relative path to the file in the Git repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `repository` - (String) The name of the helm repository.
- `revision` - (String) The branch or tag in the Git repository. 
- `values_paths` - (Block List) The relative path to the values file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))

***Optional***

- `values_ref` (Block List, Max: 1) Override relative paths to values files (see [below for nested schema](#nestedblock--spec--artifact--artifact--values_ref))

**Note**: 
- For Yaml type worklaod, `paths` is required. 
- For Helm type workload:
  - If uploading the chart, `chart_path` is required.
  - If pulling the chart from the Helm, `repository` and `chart_name` are required.
  - If pulling the chart from the Git, `repository`, `chart_path`, and `revision` are required. 


<a id="nestedblock--spec--artifact--artifact--chart_path"></a>
### Nested Schema for various resources under `spec.artifact.artifact.chart_path`, `.paths`, and `.value_paths`

***Required***

- `name` - (String) The relative path of the artifact. 


<a id="nestedblock--spec--artifact--options"></a>
### Nested Schema for `spec.artifact.options`

Optional:

***Optional***

- `atomic` - (Boolean) If enabled, the installation process deletes the installation on failure. The --wait flag is set automatically if atomic is enabled. 
- `clean_up_on_fail` - (Boolean) If enabled, cleanup deployed resources if the resource fails to deploy. 
- `description` - (String) A description for the release, provided by the user. 
- `disable_open_api_validation` - (Boolean) If enabled, disables OpenAPI validation while deploying the resource. 
- `force` - (Boolean) If enabled, deploys the resource with the force flag enabled. This will force resource updates through a replacement strategy, which destroys and recreates the resource if the upgrade fails. 
- `keep_history` - (Boolean) If enabled, it will keep the release history after uninstalling the resource. 
- `max_history` - (Number) The maximum number of historical revisions to retain. 
- `no_hooks` - (Boolean) If enabled, deploy the resource without hooks. 
- `render_sub_chart_notes` - (Boolean) If enabled, render subchart notes along with the parent. 
- `reset_values` - (Boolean) If enabled, when upgrading, reset the values to the values built into the chart. 
- `reuse_values` - (Boolean) If enabled, when upgrading, reuse the values from the last release and merge in any overrides from the command line via -set and -if. If `--reset values` is specified, this is ignored. 
- `set_string` - (List of String) If enabled, pass the custom helm values as key=value. 
- `skip_crd` - (Boolean) If enabled, the installation skips deploying CRDs. 
- `timeout` - (String) The timeout for waiting for the resources to become ready. (See [below for nested schema](#nestedblock--spec--artifact--options--timeouts)) 
- `wait` - (Boolean) If enabled, deploy the resource with wait flag. This means wait until the deployment is in a ready state before marking the release as successful. 


<a id="nestedblock--spec--drift"></a>
### Nested Schema for `spec.drift`

***Required***

- `action` - (String) If enabled, drift is enabled for resource.  Options include: `Deny` or `Notify`. 
- `enabled` - (Boolean) If enabled, drift reconciliation is enabled for resource. 


<a id="nestedblock--spec--placement"></a>
### Nested Schema for `spec.placement`

***Required****

- `labels` - (Block List; Max: 1) A list of labels for the placement. (See [below for nested schema](#nestedblock--spec--placement--labels))

<a id="nestedblock--spec--placement--labels"></a>
### Nested Schema for `spec.placement.labels`

***Required****

- `key` - (String) The key of the placement label. 
- `value` - (String) The value of the placement label. 


<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

***Optional***
- `create` - (String) Sets the timeout duration for creating a resource. The default timeout is 10 minutes. 
- `delete` - (String) Sets the timeout duration for deleting a resource. The default timeout is 10 minutes. 
- `update` - (String) Sets the timeout duration for updating a resource. The default timeout is 10 minutes. 



<a id="nestedblock--spec--artifact--artifact--values_ref"></a>
### Nested Schema for `spec.artifact.artifact.values_ref`

Optional:

- `repository` (String) name of the git repository
- `revision` (String) branch or tag in the git repository
- `values_paths` (Block List) relative path to value file in the git repository (see [below for nested schema](#nestedblock--spec--artifact--artifact--values_ref--values_paths))

<a id="nestedblock--spec--artifact--artifact--values_ref--values_paths"></a>
### Nested Schema for `spec.artifact.artifact.values_ref.values_paths`

Optional:

- `name` (String) relative path of a artifact

## Attribute Reference

---

- `id` - (String) The ID of the resource, generated by the system after you create the resource. 
