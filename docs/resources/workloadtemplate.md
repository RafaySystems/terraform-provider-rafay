---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "rafay_workloadtemplate Resource - terraform-provider-rafay"
subcategory: ""
description: |-
  
---

# rafay_workloadtemplate (Resource)

A workload template is an application that manages a set of pods.


## Example Usage

```terraform
# Create workloadtemplate of Helm package type by uploading files from local system 
resource "rafay_workloadtemplate" "tftestworkloadtemplate1" {
  metadata {
    name    = "tftestworkloadtemplate1"
    project = "tftestproject"
  }
  spec {
    artifact {
      type = "Helm"
      artifact {
        chart_path {
          name = "file://relative/path/to/some/chart.tgz"
        }
        values_paths {
          name = "file://relative/path/to/some/chart.yaml"
        }
      }
    }
    sharing {
      enabled = true
      projects {
        name = "tftestproject2"
      }
    }
  }
}

# Create workloadtemplate of Helm package type from Helm repo
resource "rafay_workloadtemplate" "tftestworkloadtemplate2" {
  metadata {
    name    = "tftestworkloadtemplate2"
    project = "tftestproject"
  }
  spec {
    artifact {
	    type = "Helm"
      artifact{
	      values_paths {
	        name = "file://relative/path/to/some/chart/values.yaml"
        }
        repository = "helm-repo-name"
        chart_name = "chartname"
	      chart_version = "versionID"
      }
    }
  }
}

# Create workloadtemplate of Helm package type from git repo
resource "rafay_workloadtemplate" "tftestworkloadtemplate3" {
  metadata {
    name    = "tftestworkloadtemplate3"
    project = "tftestproject"
  }
  spec {
    artifact {
      type = "Helm"
      artifact {
        chart_path {
          name = "relative/path/to/some/chart.yaml"
        }
        repository = "git-user-repo-name"
        revision   = "branchname"
      }
    }
  }
}

# Create a workloadtemplate of K8s Package type by uploading from local system 
resource "rafay_workloadtemplate" "tftestworkloadtemplate4" {
  metadata {
    name    = "tftestworkloadtemplate4"
    project = "tftestproject"
  }
  spec {
    artifact {
      type = "Yaml"
      artifact {
        paths {
          name = "file://relative/path/to/some/chart.yaml"
        }
      }
    }
  }
}

# Create a workload template of K8s Package type from git repo
resource "rafay_workloadtemplate" "tftestworkloadtemplate5" {
  metadata {
    name    = "tftestworkloadtemplate5"
    project = "tftestproject"
  }
  spec {
    artifact {
      type = "Yaml"
      artifact {
        paths {
          name = "relative/path/to/some/chart.yaml"
        }
        repository = "git-user-repo-name"
        revision   = "branchname"
      }
    }
  }
}
```


<!-- schema generated by tfplugindocs -->
## Argument Reference

***Required***

- `metadata` - (Block List, Max: 1) Contains data that helps uniquely identify the resource. (See [below for nested schema](#nestedblock--metadata))
- `spec` - (Block List, Max: 1) Defines the characteristics for the resource. This is the desired state for the resource. (See [below for nested schema](#nestedblock--spec))


***Optional***

- `timeouts` - (Block) Sets the duration of time the create, delete, and update functions are allowed to run. If the function takes longer than this, it is assumed the function has failed. The default is 10 minutes. (See [below for nested schema](#nestedblock--timeouts))


<a id="nestedblock--metadata"></a>
### Nested Schema for `metadata`

***Required***

- `name` - (String) The name of the resource. This must be unique in your organization. 
- `project` - (String) The name of the Rafay project the workload will be created in. 

***Optional***

- `annotations` - (Map) A key value map stored with the resource that can be used to store metadata. 
- `description` - (String) A description of the resource. 
- `labels` - (Map) A map of string keys and values for organizing and categorizing workloads. 


<a id="nestedblock--spec"></a>
### Nested Schema for `spec`

***Optional***

- `artifact` (Block List, Max: 1) The workload artifact specification. (See [below for nested schema](#nestedblock--spec--artifact))
- `drift` - (Block List, Max: 1) Prevents configuration drift. Drift is a change to your live cluster that is different from the source of truth. (See [below for nested schema](#nestedblock--spec--drift))

  **Note**: The only option is `false`. Drift configuration is currently not supported. 

- `namespace` - (String) The namespace of the workload resource. 
- `placement` - (Block List, Max: 1) Defines the cluster(s) where workload will be created. (See [below for nested schema](#nestedblock--spec--placement))
- `sharing` - (Block List, Max: 1) Enables sharing this resource with another project. (See [below for nested schema](#nestedblock--spec--sharing))
- `version` - (String) The version of the workload resource. 


<a id="nestedblock--spec--artifact"></a>
### Nested Schema for `spec.artifact`

***Optional***

- `artifact` - (Block List, Max: 1) Contains data about the artifact repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact))
- **options** - (Block List, Max: 1) ***needs info*** (See [below for nested schema](#nestedblock--spec--artifact--options))
- `type` - (String) The type of artifact. Supported values are: `Helm`, `Yaml`, and `AlertManager`. 


<a id="nestedblock--spec--artifact--artifact"></a>
### Nested Schema for `spec.artifact.artifact`

***Optional***

- `chart_name` - (String) The name of the chart. 
- `chart_path` - (Block List, Max: 1) The relative path to the chart file in the Git repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `chart_version` - (String) The version of the chart. 
- `configmap` - (Block List, Max: 1) The relative path to the alert manager configmap file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `configuration` - (Block List, Max: 1) The relative path to the alert manager configuration file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `paths` - (Block List) The relative path to the file in the Git repository. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `repository` - (String) The name of the helm repository.
- `revision` - (String) The branch or tag in the Git repository. 
- `secret` - (Block List, Max: 1) The relative path to the alert manager secret file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `statefulset` - (Block List, Max: 1) The relative path to the alert manager statefulset file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))
- `values_paths` - (Block List) The relative path to the values file. (See [below for nested schema](#nestedblock--spec--artifact--artifact--chart_path))


<a id="nestedblock--spec--artifact--artifact--chart_path"></a>
### Nested Schema for various resources under `spec.artifact.artifact.chart_path`, `.configmap`, `.configuration`, `.paths`, `.secret`, `.statefulset`, and `.value_paths`

***Optional***

- `name` - (String) The relative path of the artifact. 


<a id="nestedblock--spec--artifact--options"></a>
### Nested Schema for `spec.artifact.options`

Optional:

***Optional***

- `atomic` - (Boolean) If enabled, the installation process deletes the installation on failure. The --wait flag is set automatically if atomic is enabled. 
- `clean_up_on_fail` - (Boolean) If enabled, cleanup deployed resources if the resource fails to deploy. 
- `description` - (String) A description for the release, provided by the user. 
- `disable_open_api_validation` - (Boolean) If enabled, disables OpenAPI validation while deploying the resource. 
- `force` - (Boolean) If enabled, deploys the resource with the force flag enabled. This will force resource updates through a replacement strategy, which destroys and recreates the resource if the upgrade fails. 
- `keep_history` - (Boolean) If enabled, it will keep the release history after uninstalling the resource. 
- `max_history` - (Number) The maximum number of historical revisions to retain. 
- `no_hooks` - (Boolean) If enabled, deploy the resource without hooks. 
- `render_sub_chart_notes` - (Boolean) If enabled, render subchart notes along with the parent. 
- `reset_values` - (Boolean) If enabled, when upgrading, reset the values to the values built into the chart. 
- `reuse_values` - (Boolean) If enabled, when upgrading, reuse the values from the last release and merge in any overrides from the command line via -set and -if. If `--reset values` is specified, this is ignored. 
- `set_string` - (List of String) If enabled, pass the custom helm values as key=value. 
- `skip_crd` - (Boolean) If enabled, the installation skips deploying CRDs. 
- `timeout` - (String) The timeout for waiting for the resources to become ready. (See [below for nested schema](#nestedblock--spec--artifact--options--timeouts)) 
- `wait` - (Boolean) If enabled, deploy the resource with wait flag. This means wait until the deployment is in a ready state before marking the release as successful. 


<a id="nestedblock--spec--drift"></a>
### Nested Schema for `spec.drift`

***Required***

- `action` - (String) If enabled, drift is enabled for resource.  Options include: `Deny` or `Notify`. 
- `enabled` - (Boolean) If enabled, drift reconciliation is enabled for resource. 


<a id="nestedblock--spec--placement"></a>
### Nested Schema for `spec.placement`

***Optional***

- `labels` - (Block List; Max: 1) A list of labels for the placement. (See [below for nested schema](#nestedblock--spec--placement--labels))
- **selector** (String) Kubernetes style label selector ***needs info***


<a id="nestedblock--spec--placement--labels"></a>
### Nested Schema for `spec.placement.labels`

***Optional***

- `key` - (String) The key of the placement label. 
- `value` - (String) The value of the placement label. 


<a id="nestedblock--spec--sharing"></a>
### Nested Schema for `spec.sharing` 

***Optional***

- `enabled` - (Boolean) Enables sharing the resource with another project. 
- `projects` - (Block List; Max: 1) The name of the project to share the resource with. 


<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

***Optional***
- `create` - (String) Sets the timeout duration for creating a resource. The default timeout is 10 minutes. 
- `delete` - (String) Sets the timeout duration for deleting a resource. The default timeout is 10 minutes. 
- `update` - (String) Sets the timeout duration for updating a resource. The default timeout is 10 minutes. 


## Attribute Reference

---

- `id` - (String) The ID of the resource, generated by the system after you create the resource. 