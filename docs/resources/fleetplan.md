---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "rafay_fleetplan Resource - terraform-provider-rafay"
subcategory: ""
description: |-
  
---

# rafay_fleetplan (Resource)



## Example Usage 

### Example 1: Fleet Plan with Named Action

```terraform
resource "rafay_fleetplan" "demo-fleetplan" {
    metadata {
        name    = "demo-fleetplan"
        project = "terraform1"
    }
    spec {
        fleet {
            kind = "clusters"
            labels = {
                "key1" = "value1"
                "key2" = "value2"
            }

            projects {
                name = "terraform1"
            }
            projects {
                name = "terraform2"
            }
        }

        operation_workflow {
            operations {
                name = "upgrade-cp-ng"
                action {
                    type        = "nodeGroupsAndControlPlaneUpgrade"
                    description = "upgrading control plane  and nodegroup"
                    node_groups_and_control_plane_upgrade_config {
                        version = "1.25"
                    }
                    name = "upgrade-both-cpng"
                }
                prehooks {
                    name = "cpngup-prehook1"
                    description = "list all nodes before upgrade"
                    container_config {
                        runner {
                           type = "cluster"
                        }
                        image = "bitnami/kubectl"
                        arguments = ["get", "nodes", "-A"]
                    }
                }
            }
        }
    }
}
```

### Example 2: Fleet Plan with Patch Action

```terraform
resource "rafay_fleetplan" "demo-fleetplan" {
    metadata {
        name    = "demo-fleetplan"
        project = "terraform1"
    }
    spec {
        fleet {
            kind = "clusters"
            labels = {
                "key1" = "value1"
                "key2" = "value2"
            }

            projects {
                name = "terraform1"
            }
            projects {
                name = "terraform2"
            }
        }

        operation_workflow {
            operations {
                name = "upgrade-ng-only"
                action {
                    type        = "patch"
                    description = "upgrading nodegroup"
                    patch_config {
                        op = "replace"
                        path = ".spec.config.managedNodeGroups[1].ami"
                        value = jsonencode("ami-015a336f2a25fc752")
                    }
                    name = "action2"
                }
                prehooks {
                    name = "ngupg-prehook1"
                    description = "list all nodes before upgrade"
                    container_config {
                        runner {
                            type = "cluster"
                        }
                        image = "bitnami/kubectl"
                        arguments = ["get", "nodes", "-A"]
                    }
                }
                posthooks {
                    name = "ngupg-posthook1"
                    description = "list all nodes after upgrade"
                    container_config {
                        runner {
                            type = "cluster"
                        }
                        image = "bitnami/kubectl"
                        arguments = ["get", "nodes", "-A"]
                    }
                }
            }
        }
        agents {
           name = "demoagent"
      }
    }
}
```


<!-- schema generated by tfplugindocs -->
## Schema

***Required***

- `metadata` - (Block List, Max: 1) Contains data that helps uniquely identify the resource. (See [below for nested schema](#nestedblock--metadata))
- `spec` - (Block List, Max: 1) Defines the characteristics for the resource. This is the desired state for the resource. (See [below for nested schema](#nestedblock--spec))


<a id="nestedblock--metadata"></a>
### Nested Schema for `metadata`

***Required***

- `name` - (String) The name of the Fleet Plan. This must be unique in your organization. 
- `project` - (String) The name of the Rafay project the Fleet Plan will be created in.


<a id="nestedblock--spec"></a>
### Nested Schema for `spec`

***Required***
- `agents` (Block List) gitops agent required for hooks with runner type as agent  (see [below for nested schema](#nestedblock--spec--agents))
- `fleet` (Block List, Max: 1) This block specifies the kind of Fleet (e.g., "clusters")  (see [below for nested schema](#nestedblock--spec--fleet))
- `operation_workflow` (Block List, Max: 1)  (see [below for nested schema](#nestedblock--spec--operation_workflow))


<a id="nestedblock--spec--agents"></a>
### Nested Schema for `spec.agents`

If the runner type in the hooks configuration is set to agent, then this field is **mandatory**.

- `name` (String)   The name of the agent.


<a id="nestedblock--spec--fleet"></a>
### Nested Schema for `spec.fleet`

***Required***

- `kind` (String)  Specifies the kind of Fleet. The supported value is `clusters`
- `labels` (Map of String) This map allows you to attach key-value labels to the Fleet for selecting clusters.
- `projects` (Block List) This block allows you to specify one or more projects, and you can select clusters from these specified projects. (see [below for nested schema](#nestedblock--spec--fleet--projects))

<a id="nestedblock--spec--fleet--projects"></a>
### Nested Schema for `spec.fleet.projects`

***Required***

- `name` (String) Name of the project this fleet plan will act on applicable clusters.

<a id="nestedblock--spec--operation_workflow"></a>
### Nested Schema for `spec.operation_workflow`

***Required***

- `operations` (Block List) (see [below for nested schema](#nestedblock--spec--operation_workflow--operations))

<a id="nestedblock--spec--operation_workflow--operations"></a>
### Nested Schema for `spec.operation_workflow.operations`


***Required***

- `name` (String) The name of the operation.
- `action` (Block List, Max: 1)  Details of the action to perform. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--action))
 

Optional:

- `prehooks` (Block List) Pre-hooks to execute before the action. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--prehooks))
- `posthooks` (Block List) Post-hooks to execute after the action. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--posthooks))


<a id="nestedblock--spec--operation_workflow--operations--action"></a>
### Nested Schema for `spec.operation_workflow.operations.action`

***Required*** 

- `name` (String) The name of the action.
- `type` (String) The type of action. The supported values are `controlPlaneUpgrade`, `nodeGroupsUpgrade`, `nodeGroupsAndControlPlaneUpgrade` and `patch`

**Note**:
- One of the following configuration is required based on value of of the `type`

- `control_plane_upgrade_config` (Block List, Max: 1) Configuration for control plane upgrade action, used for upgrading the control plane only. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--prehooks--control_plane_upgrade_config))
- `node_groups_upgrade_config` (Block List, Max: 1)  Configuration for node group upgrade action, used for upgrading the node group only (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--prehooks--node_groups_upgrade_config))
- `node_groups_and_control_plane_upgrade_config` (Block List, Max: 1)  Configuration for control plane and node group upgrade action, used for upgrading the both    (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--prehooks--node_groups_and_control_plane_upgrade_config))
- `patch_config` (Block List, Max: 1) A cluster configuration file can be partially modified using a patch operation. YAML patch is used to update & modify specific fields in the configuration file. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--action--patch_config))

***Optional***

- `description` (String) Description of action

<a id="nestedblock--spec--operation_workflow--operations--prehooks--control_plane_upgrade_config"></a>
### Nested Schema for `spec.operation_workflow.operations.prehooks.control_plane_upgrade_config`

***Required***

- `version` (String) kubernetes version required to be updated.


<a id="nestedblock--spec--operation_workflow--operations--prehooks--node_groups_and_control_plane_upgrade_config"></a>
### Nested Schema for `spec.operation_workflow.operations.prehooks.node_groups_and_control_plane_upgrade_config`

***Required***

- `version` (String) kubernetes version required to be updated. 


<a id="nestedblock--spec--operation_workflow--operations--prehooks--node_groups_upgrade_config"></a>
### Nested Schema for `spec.operation_workflow.operations.prehooks.node_groups_upgrade_config`

***Required***

- `names` (List of String) Name of the nodegroup(s)
- `version` (String) kubernetes version required to be updated. 


<a id="nestedblock--spec--operation_workflow--operations--action--patch_config"></a>
### Nested Schema for `spec.operation_workflow.operations.action.patch_config`

***Required***

- `op` (String): The operation to perform. The supported values is `replace`
- `path` (String)  The configuration path where the patch should be applied.  
   -  Example: `".spec.config.managedNodeGroups[1].ami"`
- `value` (String)  Desired value required to be replaced.

<a id="nestedblock--spec--operation_workflow--operations--prehooks"></a>
### Nested Schema for `spec.operation_workflow.operations.prehooks`

***Required***

- `name` (String) The name of the pre-hook.
- `container_config` (Block List, Max: 1) Configuration for the container to run. (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--prehooks--container_config))

***Optional***

- `description` (String) A description of the pre-hook.
- `inject` (List of String): Specifies environment variables to inject into the container. By default, only `KUBECONFIG` is available, which can be used to set up the connection to the target clusters.

<a id="nestedblock--spec--operation_workflow--operations--prehooks--container_config"></a>
### Nested Schema for `spec.operation_workflow.operations.prehooks.container_config`

***Required***

- `image` (String) The container image to use.
   - Example: `"bitnami/kubectl"`
- `runner` (String) The type of runner. The supported values are `cluster` and `agent`
   - Example: `"cluster"` The container will run on all the targeted clusters for pre-hook operations.
   - Example: `"agent"` The container will run on the selected agent for pre-hook operations.

***Optional***

- `arguments` (List of String) Arguments to pass to the container.
   - Example: `["get", "nodes", "-A"]
- `commands` (List of String) Specifies the commands to execute within the container.
- `env` (Map of String)  Pass variables to the pre-hook script that will be available at runtime.
   - Example: `"key1:value1"`

<a id="nestedblock--spec--operation_workflow--operations--posthooks"></a>
### Nested Schema for `spec.operation_workflow.operations.posthooks`

***Required***

- `name` (String) The name of the post-hook.
- `container_config` (Block List, Max: 1) (see [below for nested schema](#nestedblock--spec--operation_workflow--operations--posthooks--container_config))

***Optional***

- `description` (String) Description of the post-hook.
- `inject` (List of String) Specifies environment variables to inject into the container. By default, only `KUBECONFIG` is available, which can be used to set up the connection to the target clusters.

<a id="nestedblock--spec--operation_workflow--operations--posthooks--container_config"></a>
### Nested Schema for `spec.operation_workflow.operations.posthooks.container_config`

***Required***

- `image` (String) The container image to use.
- `runner` (String) The type of runner. The supported values are `cluster` and `agent`
   - Example: `"cluster"` The container will run on all the targeted clusters for post-hook operations.
   - Example: `"agent"` The container will run on the selected agent for post-hook operations.

***Optional***

- `arguments` (List of String)  Arguments to pass to the container.
- `commands` (List of String) Specifies the commands to execute within the container.
- `env` (Map of String) Pass variables to the post-hook script that will be available at runtime.
  - Example: `"key1:value1"`

